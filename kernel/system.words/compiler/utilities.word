; @codeonly

; ***********************************************************************************************
;
;									Generate code for constant in HL
;
; ***********************************************************************************************

COMUTLConstantCode:
		ld 		a,$EB 								; ex de,hl
		call 	FARCompileByte 
		ld 		a,$21 								; ld hl,const
		call 	FARCompileByte
		call 	FARCompileWord 						; compile the constant
		ret

; ***********************************************************************************************
;
;				Compile code to call EHL from current compile position
;
; ***********************************************************************************************

COMUTLCodeCallEHL:
		ld 		a,$CD 								; call <Address>
		call 	FARCompileByte
		call 	FARCompileWord 						; compile the constant
		ret

; ***********************************************************************************************
;
;									Execute code at EHL 
;
; ***********************************************************************************************

COMUTLExecuteEHL:
		ld 		a,e 								; switch to that page
		call 	PAGESwitch
		ld 		de,COMUTLExecuteExit 				; push after code address
		push 	de
		push 	hl 									; push call address
		ld 		hl,(ARegister) 						; load registers
		ld 		de,(BRegister)
		ld 		bc,(CRegister)
		ret 										; execute the call
COMUTLExecuteExit:
		ld 		(CRegister),bc 						; save registers
		ld 		(BRegister),de
		ld 		(ARegister),hl
		call 	PAGERestore
		ret
		
; ***********************************************************************************************
;
;		Copy a macro. The return address points to ld a,<count> followed by the macro contents
;		Note only the lower 4 bits of the count are valid.
;
; ***********************************************************************************************

COMHCopyFollowingCode:
		pop 	hl 										; get return address
		ld 		a,(hl) 									; get count in bits 0..3
		and 	15
		ld 		b,a
__COMHCFCLoop: 											; copy bytes
		inc 	hl
		ld 		a,(hl)
		call 	FARCompileByte
		djnz 	__COMHCFCLoop
		ret

; ***********************************************************************************************
;
;			Create a call to the code following this caller. It is running in page A'
;
; ***********************************************************************************************

COMHCreateCallToCode:
		pop 	hl 										; get the address of the code.
		call 	COMUTLCodeCallEHL 						; compile a call to E:HL from here. 	
		ret
